[ai_queue3,cannon_boss] @cannon_boss_drop_table;

[label,cannon_boss_drop_table]
gosub(npc_death);

def_namedobj $d1;
def_namedobj $d2;
def_namedobj $d3;
def_namedobj $r1;
def_int $count = 1;

$d1, $d2, $d3, $r1, $count = ~prince_ali_boss_loot;

obj_addall(npc_coord, $d1, 1, ^lootdrop_duration);
obj_addall(npc_coord, $d2, 1, ^lootdrop_duration);
obj_addall(npc_coord, $d3, 1, ^lootdrop_duration);
obj_addall(npc_coord, $r1, $count, ^lootdrop_duration);

announce("Nulodion: I needed dragon balls.");
announce("Nulodion has been slain.");

[ai_timer,cannon_boss]
switch_int(random(8)) {
    case 0 : npc_say("Cannon!");
    case 1 : npc_say("Fire!");
    case 2 : npc_say("Shoot!");
    case 3 : npc_say("I'm not short!");
    case 4 : npc_say("Hammer time!");
    case 5 : npc_say("I'm going to steel your life!");
    case 6 : npc_say("I am the creator of the Dwarf MultiCannon you know.");
    case 7 : npc_say("Fresh out the mining guild, you are.");
}

huntall(npc_coord, 20, 0);
while (huntnext = true) {
     ~cannon_boss_cannon_fire;
}

[proc,cannon_boss_cannon_fire]
def_int $duration = ~player_projectile(npc_coord, coord, uid, cannonball_travel, 36, 35, 0, 2, 35, 0, 5);

~sound_within_distance(mcannon_fire, 0, coord, 10);

def_int $damage = 0;
$damage = randominc(30);

npc_say("FIRE!");
queue(combat_damage_player, 0, $damage);

